#!/usr/bin/env coffee
###
BEAUTIFUL DOCS
Copyright (C) 2012 Maxime Bouroumeau-Fuseau
###

fs = require 'fs'
path = require 'path'
_ = require 'underscore'

argv = []
options =
    server: false
    watch: false
    port: 8080
    many: false

for arg in process.argv.slice(2)
    if arg.substr(0, 2) == '--'
        parts = arg.split '='
        v = parts[1] || true
        v = false if v == 'false'
        options[parts[0].substr(2).replace('-', '_')] = v
    else
        argv.push arg

if argv.length == 0
    console.log 'At least one manifest must be specified'
    process.exit(1)

bfdocs = require '..'

destDir = './out'
if argv.length > 1 and not options.many
    destDir = _.last(argv)
    argv = _.initial(argv)
manifestFilenames = argv

generatorOptions = 
    title: options.title || 'Beautiful Docs'
    baseUrl: options.base_url || ''
    displayHeader: options.display_header ? true

lock = manifestFilenames.length
manifests = []

generateManifest = (manifest, callback=null) ->
    dest = destDir
    opts = _.extend({}, generatorOptions)
    if manifestFilenames.length > 1
        dest = path.join destDir, manifest.slug
        _.extend opts, {baseUrl: generatorOptions.baseUrl + '/' + manifest.slug}
    bfdocs.generate manifest, dest, opts, callback

generateIndex = (callback=null) ->
    bfdocs.generateIndex options.title || 'Beautiful docs', 
        manifests, path.join(destDir, 'index.html'), generatorOptions, callback

startServer = (err) ->
    if options.server
        console.log "Starting server on port #{options.port}"
        bfdocs.serveStaticDir destDir, options.port
    else if options.watch
        console.log "Press Ctrl+C to quit"
        setInterval (-> 1), 100

handleManifest = (err, manifest) ->
    if err
        console.log "An error occured while generating a manifest: #{err}"
        return
    manifests.push manifest
    generateManifest manifest, (err) ->
        if err
            lock--
            console.log "An error occured while generating a manifest: #{err}"
            return
        if options.watch
            manifest.watch -> 
                console.log "Changes detected to '#{manifest.uri}'"
                generateManifest manifest
        if --lock == 0
            if manifestFilenames.length > 1
                generateIndex startServer
            else
                startServer()

for manifestFilename in manifestFilenames
    if fs.statSync(manifestFilename).isDirectory()
        console.log "Generating documentation from directory '#{manifestFilename}'"
        bfdocs.createManifestFromDir manifestFilename, handleManifest
    else
        console.log "Generating documentation from manifest '#{manifestFilename}'"
        bfdocs.open manifestFilename, handleManifest
